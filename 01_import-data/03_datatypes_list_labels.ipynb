{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "87d76f26-2eac-4841-abbf-2846388090cf",
   "metadata": {},
   "source": [
    "# Casting\n",
    "\n",
    "All data loaded using LOAD CSV will be returned as strings - you need to cast the data to an appropriate data type before being written to a property.\n",
    "\n",
    "The types of data that you can store as properties in Neo4j include:\n",
    "\n",
    "- String\n",
    "- Integer\n",
    "- Float (decimal values)\n",
    "- Boolean\n",
    "- Date/Datetime\n",
    "- Point (spatial)\n",
    "- Lists of values\n",
    "\n",
    "Cypher functions to cast data include:\n",
    "\n",
    "\n",
    "| Function | Description |\n",
    "|----------|-------------|\n",
    "| `toBoolean()` | Converts a string to a boolean value |\n",
    "| `toFloat()` | Converts a string to a float value |\n",
    "| `toInteger()` | Converts a string to an integer value |\n",
    "| `toString()` | Converts a value to a string |\n",
    "| `date()` | Converts a string to a date value |\n",
    "| `datetime()` | Converts a string to a date and time value |\n",
    "\n",
    "You can use the `apoc.meta.nodeTypeProperties()` function to show the data types used in the graph:\n",
    "\n",
    "```cypher\n",
    "CALL apoc.meta.nodeTypeProperties()\n",
    "YIELD nodeType, propertyName, propertyTypes\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1c03b6ad-2a73-4b27-b046-ab7a90b968bc",
   "metadata": {},
   "source": [
    "## Person node dates\n",
    "\n",
    "You used this Cypher statement to create the Person nodes:\n",
    "\n",
    "```cypher\n",
    "LOAD CSV WITH HEADERS FROM 'https://data.neo4j.com/importing-cypher/persons.csv' AS row\n",
    "MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})\n",
    "SET\n",
    "p.imdbId = toInteger(row.person_imdbId),\n",
    "p.bornIn = row.bornIn,\n",
    "p.name = row.name,\n",
    "p.bio = row.bio,\n",
    "p.poster = row.poster,\n",
    "p.url = row.url,\n",
    "p.born = row.born,\n",
    "p.died = row.died\n",
    "```\n",
    "\n",
    "Correct the Person nodes. Run this updated query to modify the born and died properties to be Date values.\n",
    "\n",
    "```cypher\n",
    "LOAD CSV WITH HEADERS FROM 'https://data.neo4j.com/importing-cypher/persons.csv' AS row\n",
    "MERGE (p:Person {tmdbId: toInteger(row.person_tmdbId)})\n",
    "SET\n",
    "p.imdbId = toInteger(row.person_imdbId),\n",
    "p.bornIn = row.bornIn,\n",
    "p.name = row.name,\n",
    "p.bio = row.bio,\n",
    "p.poster = row.poster,\n",
    "p.url = row.url,\n",
    "p.born = date(row.born),\n",
    "p.died = date(row.died)\n",
    "```\n",
    "\n",
    "**Using MERGE not CREATE?**\n",
    "As `MERGE` was used in this Cypher statement, you can run it multiple times without creating duplicate nodes. It will update the existing nodes with the new date values. If you used `CREATE` instead, you would create new nodes each time you ran the statement.\n",
    "\n",
    "The Date data type allows you to extract the year, month, and day from the date. For example,\n",
    "\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "RETURN p.born.year as YearOfBirth\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cc6328c1-83fe-48c2-b750-3cdd406f1df1",
   "metadata": {},
   "source": [
    "# Adding Properties\n",
    "\n",
    "Previously, you used the following Cypher statement to import the `Movie` nodes:\n",
    "\n",
    "```cypher\n",
    "LOAD CSV WITH HEADERS\n",
    "FROM 'https://data.neo4j.com/importing-cypher/movies.csv'\n",
    "AS row\n",
    "MERGE (m:Movie {movieId: toInteger(row.movieId)})\n",
    "SET\n",
    "m.tmdbId = toInteger(row.movie_tmdbId),\n",
    "m.imdbId = toInteger(row.movie_imdbId),\n",
    "m.released = row.released,\n",
    "m.title = row.title,\n",
    "m.year = row.year,\n",
    "m.plot = row.plot,\n",
    "m.budget = row.budget\n",
    "```\n",
    "\n",
    "The year and budget properties are not cast and therefore stored as strings. Update and run the existing import to cast the year and budget properties to integers using the toInteger() function.\n",
    "\n",
    "```cypher\n",
    "LOAD CSV WITH HEADERS\n",
    "FROM 'https://data.neo4j.com/importing-cypher/movies.csv'\n",
    "AS row\n",
    "MERGE (m:Movie {movieId: toInteger(row.movieId)})\n",
    "SET\n",
    "m.tmdbId = toInteger(row.movie_tmdbId),\n",
    "m.imdbId = toInteger(row.movie_imdbId),\n",
    "m.released = row.released,\n",
    "m.title = row.title,\n",
    "m.year = toInteger(row.year),\n",
    "m.plot = row.plot,\n",
    "m.budget = toInteger(row.budget)\n",
    "```\n",
    "\n",
    "The movies.csv file also contains the following additional properties:\n",
    "\n",
    "| Property | Data type | Cast function |\n",
    "|----------|-----------|---------------|\n",
    "| imdbRating | Float | `toFloat()` |\n",
    "| poster | String | |\n",
    "| runtime | Integer | `toInteger()` |\n",
    "| imdbVotes | Integer | `toInteger()` |\n",
    "| revenue | Integer | `toInteger()` |\n",
    "| url | String | |"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "991447ca-b31c-4ae0-9bf3-317b350aad0e",
   "metadata": {},
   "source": [
    "# Lists\n",
    "\n",
    "**What is a Multi-value property?**\n",
    "\n",
    "A multi-value property is a property that can hold one or more values. Neo4j represents this type of data as a list (or \"StringArray\").\n",
    "\n",
    "All values in a list must have the same data type. For example:\n",
    "\n",
    "    [\"Apple\", \"Banana, \"Orange\"]\n",
    "\n",
    "    [100, 55, 4]\n",
    "\n",
    "The movies.csv data file contains multi-value properties, including:\n",
    "\n",
    "- countries - the countries which produced the movie\n",
    "\n",
    "- languages - the languages spoken in the movie\n",
    "\n",
    "Find the countries and languages data in the movies.csv file. You will see that each property contains a list of values separated by a | character. For example:\n",
    "\n",
    "```\n",
    "USA|France|Italy|Germany\n",
    "English|Mandarin|Russian\n",
    "```\n",
    "\n",
    "## Split the data into a list\n",
    "\n",
    "This updated Movie import creates a list of countries by using the split function:\n",
    "\n",
    "```cypher\n",
    "LOAD CSV WITH HEADERS\n",
    "FROM 'https://data.neo4j.com/importing-cypher/movies.csv'\n",
    "AS row\n",
    "MERGE (m:Movie {movieId: toInteger(row.movieId)})\n",
    "SET\n",
    "m.tmdbId = toInteger(row.movie_tmdbId),\n",
    "m.imdbId = toInteger(row.movie_imdbId),\n",
    "m.released = date(row.released),\n",
    "m.title = row.title,\n",
    "m.year = toInteger(row.year),\n",
    "m.plot = row.plot,\n",
    "m.budget = toInteger(row.budget),\n",
    "m.imdbRating = toFloat(row.imdbRating),\n",
    "m.poster = row.poster,\n",
    "m.runtime = toInteger(row.runtime),\n",
    "m.imdbVotes = toInteger(row.imdbVotes),\n",
    "m.revenue = toInteger(row.revenue),\n",
    "m.url = row.url,\n",
    "m.countries = split(row.countries, '|')\n",
    "```\n",
    "\n",
    "The statement sets the countries property as a list by splitting the data from the CSV file by the | character.\n",
    "\n",
    "Run the query and inspect the countries property data by writing a `MATCH` query to return the data.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE \"France\" IN m.countries\n",
    "RETURN m\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "64ef0601-2c77-42a6-ab74-f28f2d3c8506",
   "metadata": {},
   "source": [
    "# Add Labels to Existing Nodes\n",
    "\n",
    "## Add Actor label\n",
    "\n",
    "Adding labels to existing nodes can make your graph more useful and performant.\n",
    "\n",
    "The Person nodes in the graph represent both actors and directors. To determine if a person is an actor or director, you need to query the `ACTED_IN` or `DIRECTED` relationships. Alternatively, you could add labels to the existing nodes to distinguish between actors and directors.\n",
    "\n",
    "You can add labels to a node using SET, the syntax is:\n",
    "\n",
    "```cypher\n",
    "MATCH (n)\n",
    "SET n:Label\n",
    "```\n",
    "\n",
    "The following query would add the `Actor` label to all `Person` nodes that have acted in a movie:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->()\n",
    "SET p:Actor\n",
    "```\n",
    "\n",
    "As there are people in the database who have acted in more than one movie, you can use `WITH DISTINCT` to ensure that each person is only labeled once. Although not essential, this will improve the performance of the statement.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->()\n",
    "WITH DISTINCT p SET p:Actor\n",
    "```\n",
    "\n",
    "You can confirm it was successful by using the Actor label to find actors in the graph:\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Actor) RETURN a LIMIT 25\n",
    "````\n",
    "\n",
    "## Add Director label\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->()\n",
    "WITH DISTINCT p SET p:Actor\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d8eac927-5e82-4bef-89e8-31e1e2463781",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
