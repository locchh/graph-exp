{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "091ca0dc-7675-4daa-987c-219fbcc81e65",
   "metadata": {},
   "source": [
    "# Filtering Queries\n",
    "\n",
    "There are many ways that you can query a subset or subgraph of the data in the graph.\n",
    "\n",
    "In this module, you will:\n",
    "\n",
    "- Review basic query filtering.\n",
    "\n",
    "- Evaluate strings in your query filtering.\n",
    "\n",
    "- Retrieve data using patterns in the graph.\n",
    "\n",
    "- Learn how to specify multiple or optional MATCH clauses.\n",
    "\n",
    "## Filtering with WHERE\n",
    "\n",
    "### 1. Testing Equality\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Tom Hanks'\n",
    "AND m.year = 2013\n",
    "RETURN m.title\n",
    "```\n",
    "\n",
    "### 2. Testing Inequality\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name <> 'Tom Hanks'\n",
    "AND m.title = 'Captain Phillips'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "### 3. Testing less than or greater than\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie) WHERE m.title = 'Toy Story'\n",
    "RETURN\n",
    "    m.year < 1995 AS lessThan, //  Less than (false)\n",
    "    m.year <= 1995 AS lessThanOrEqual, // Less than or equal(true)\n",
    "    m.year > 1995 AS moreThan, // More than (false)\n",
    "    m.year >= 1995 AS moreThanOrEqual // More than or equal (true)\n",
    "```\n",
    "\n",
    "### 4. Testing Ranges\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Tom Hanks'\n",
    "AND  2005 <= m.year <= 2010\n",
    "RETURN m.title, m.released\n",
    "```\n",
    "\n",
    "### 5. Testing null property values\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.died IS NOT NULL\n",
    "AND p.born.year >= 1985\n",
    "RETURN p.name, p.born, p.died\n",
    "```\n",
    "\n",
    "And we can test if a property exists using the IS NULL predicate:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.died IS NULL\n",
    "AND p.born.year <= 1922\n",
    "RETURN p.name, p.born, p.died\n",
    "```\n",
    "\n",
    "### 6. Testing labels or patterns?\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE  p.born.year > 1960\n",
    "AND p:Actor\n",
    "AND p:Director\n",
    "RETURN p.name, p.born, labels(p)\n",
    "```\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p)\n",
    "WHERE  p.born.year > 1960\n",
    "RETURN p.name, p.born, labels(p), m.title\n",
    "````\n",
    "\n",
    "### 7. Testing list inclusion\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE \"Israel\" IN m.countries\n",
    "RETURN m.title, m.languages, m.countries\n",
    "```\n",
    "\n",
    "```\n",
    "MATCH (d:Director)-[:DIRECTED]->(m:Movie)-[:IN_GENRE]->(g:Genre)\n",
    "WHERE m.year = 2000 AND g.name = \"Horror\"\n",
    "RETURN d.name\n",
    "```\n",
    "\n",
    "### 8. Testing strings\n",
    "\n",
    "When the property is a string type, you can filter by the starting characters in the string:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE  m.title STARTS WITH 'Toy Story'\n",
    "RETURN m.title, m.released\n",
    "```\n",
    "\n",
    "And you can filter queries whose properties end with a set of characters:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE  m.title ENDS WITH ' I'\n",
    "RETURN m.title, m.released\n",
    "```\n",
    "\n",
    "Additionally, you can test if a substring is contained in a property:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE  m.title CONTAINS 'River'\n",
    "RETURN m.title, m.released\n",
    "```\n",
    "\n",
    "Case-sensitive strings\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE toLower(p.name) ENDS WITH 'demille'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "Conversely, we can do the same test with upper-case:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE toUpper(p.name) ENDS WITH 'DEMILLE'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "### 8. About indexes for queries\n",
    "\n",
    "If you transform a string property during a query, such as `toUpper()` or `toLower()`, the query engine turns off the use of the index.\n",
    "\n",
    "With any query, you can always check if an index will be used by prefixing the query with `EXPLAIN`.\n",
    "\n",
    "```cypher\n",
    "EXPLAIN MATCH (m:Movie)\n",
    "WHERE  m.title STARTS WITH 'Toy Story'\n",
    "RETURN m.title, m.released\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "45a2541e-0e70-48c3-b662-1b3bffda9566",
   "metadata": {},
   "source": [
    "# Query Patterns and Performance\n",
    "\n",
    "## Patterns in the graph\n",
    "\n",
    "A pattern is a combination of nodes and relationships that is used to traverse the graph at runtime. You can write queries that test whether a pattern exists in the graph.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE  p.name = 'Tom Hanks'\n",
    "AND exists {(p)-[:DIRECTED]->(m)}\n",
    "RETURN p.name, labels(p), m.title\n",
    "```\n",
    "\n",
    "This `exists { }` test is done for every Movie node related to Tom Hanks as an actor. This query returns the single movie that Tom Hanks directed and acted in.\n",
    "\n",
    "### Profiling queries\n",
    "\n",
    "You can use the `PROFILE` keyword to show the total number of rows retrieved from the graph in the query.\n",
    "\n",
    "```cypher\n",
    "PROFILE MATCH (p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p)\n",
    "WHERE  p.name = 'Tom Hanks'\n",
    "RETURN  m.title\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "07075148-ee8f-4f40-b5f7-bb1a29e8076e",
   "metadata": {},
   "source": [
    "# Multiple MATCH Clauses\n",
    "\n",
    "## Using multiple MATCH clauses\n",
    "\n",
    "Here is an example of a query that contains two explicit `MATCH` clauses:\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE m.year > 2000\n",
    "MATCH (m)<-[:DIRECTED]-(d:Person)\n",
    "RETURN a.name, m.title, d.name\n",
    "```\n",
    "\n",
    "An alternative to using multiple MATCH clauses is to specify multiple patterns:\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie),\n",
    "      (m)<-[:DIRECTED]-(d:Person)\n",
    "WHERE m.year > 2000\n",
    "RETURN a.name, m.title, d.name\n",
    "```\n",
    "\n",
    "Or using a single pattern:\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)\n",
    "WHERE m.year > 2000\n",
    "RETURN a.name, m.title, d.name\n",
    "```\n",
    "\n",
    "In general, using a single MATCH clause will perform better than multiple MATCH clauses. This is because relationship uniqueness is enforced so there are fewer relationships traversed.\n",
    "\n",
    "## Optionally matching rows\n",
    "\n",
    "Cypher has a clause that allows you to return rows that contain null values for some properties.\n",
    "\n",
    "Here is an example of a query that we will start with:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie) WHERE m.title = \"Kiss Me Deadly\"\n",
    "MATCH (m)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)\n",
    "MATCH (m)<-[:ACTED_IN]-(a:Actor)-[:ACTED_IN]->(rec)\n",
    "RETURN rec.title, a.name\n",
    "```\n",
    "\n",
    "In this query:\n",
    "\n",
    "1. We find the movie node for Kiss Me Deadly.\n",
    "\n",
    "2. Then we find all movies, rec that are in the same genre as Kiss Me Deadly.\n",
    "\n",
    "3. Then we find the actors that acted in both rec and Kiss Me Deadly.\n",
    "\n",
    "What if we wanted to expand the results returned to return all movies that are in the same genre, and the actor that acted in both movies. `OPTIONAL MATCH` matches patterns with your graph, just like `MATCH` does. The difference is that if no matches are found, `OPTIONAL MATCH` will use nulls for missing parts of the pattern. `OPTIONAL MATCH` could be considered the Cypher equivalent of the outer join in SQL.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie) WHERE m.title = \"Kiss Me Deadly\"\n",
    "MATCH (m)-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(rec:Movie)\n",
    "OPTIONAL MATCH (m)<-[:ACTED_IN]-(a:Actor)-[:ACTED_IN]->(rec)\n",
    "RETURN rec.title, a.name\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
