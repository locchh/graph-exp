{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "1b5df5b6-242a-44c2-bf27-1047a77068d9",
   "metadata": {},
   "source": [
    "## Creating Nodes\n",
    "\n",
    "Suppose we want to create a node to represent Michael Caine. Run this Cypher code to create the node. It creates a single node in the graph. Note that when you use MERGE to create a node, you must specify at least one property that will be the unique primary key for the node.\n",
    "\n",
    "```cypher\n",
    "MERGE (p:Person {name: 'Michael Caine'})\n",
    "```\n",
    "\n",
    "Verify that the node was created.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Michael Caine'})\n",
    "RETURN p\n",
    "```\n",
    "\n",
    "We can also chain multiple `MERGE` clauses together within a single Cypher code block.\n",
    "\n",
    "```cypher\n",
    "MERGE (p:Person {name: 'Katie Holmes'})\n",
    "MERGE (m:Movie {title: 'The Dark Knight'})\n",
    "RETURN p, m\n",
    "```\n",
    "\n",
    "### Using CREATE instead of MERGE to create nodes\n",
    "\n",
    "Cypher has a `CREATE` clause you can use for creating nodes. The benefit of using `CREATE` is that it does not look up the primary key before adding the node. You can use `CREATE` if you are sure your data is clean and you want greater speed during import. "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "94ac16cb-77ec-45ef-af88-fb7874c26842",
   "metadata": {},
   "source": [
    "## Creating Relationships\n",
    "\n",
    "Just like you can use MERGE to create nodes in the graph, you use MERGE to create relationships between two nodes. First you must have references to the two nodes you will be creating the relationship for. When you create a relationship between two nodes, it must have:\n",
    "- Type\n",
    "- Direction\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Michael Caine'})\n",
    "MATCH (m:Movie {title: 'The Dark Knight'})\n",
    "MERGE (p)-[:ACTED_IN]->(m)\n",
    "```\n",
    "\n",
    "Notice also that you need not specify direction in the MATCH pattern since the query engine will look for all nodes that are connected, regardless of the direction of the relationship.\n",
    "\n",
    "Creating nodes and relationships using multiple clauses\n",
    "\n",
    "```cypher\n",
    "MERGE (p:Person {name: 'Chadwick Boseman'})\n",
    "MERGE (m:Movie {title: 'Black Panther'})\n",
    "MERGE (p)-[:ACTED_IN]-(m)\n",
    "```\n",
    "\n",
    "Using MERGE to create nodes and a relationship in single clause\n",
    "\n",
    "```cypher\n",
    "MERGE (p:Person {name: 'Emily Blunt'})-[:ACTED_IN]->(m:Movie {title: 'A Quiet Place'})\n",
    "RETURN p, m\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c55ea0cd-5ab9-4ac2-83b5-76222c4b0474",
   "metadata": {},
   "source": [
    "## Adding properties for a node or relationship\n",
    "\n",
    "Thus far, you have learned how to create nodes with MERGE where you specify the primary key property for the node. You can add, modify, or remove properties from nodes and relationships.\n",
    "\n",
    "In this lesson you will learn how to write Cypher code to update properties of nodes and relationships.\n",
    "\n",
    "There are two ways that you can set a property for a node or relationship.\n",
    "\n",
    "### 1. Inline as part of the MERGE clause\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Michael Caine'})\n",
    "MERGE (m:Movie {title: 'Batman Begins'})\n",
    "MERGE (p)-[:ACTED_IN {roles: ['Alfred Penny']}]->(m)\n",
    "RETURN p,m\n",
    "```\n",
    "\n",
    "### 2. Using the SET keyword for a reference to a node or relationship\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'\n",
    "SET r.roles = ['Alfred Penny']\n",
    "RETURN p, r, m\n",
    "```\n",
    "\n",
    "## Setting multiple properties\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'\n",
    "SET r.roles = ['Alfred Penny'], m.released = 2008\n",
    "RETURN p, r, m\n",
    "```\n",
    "\n",
    "## Updating properties\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'\n",
    "SET r.roles = ['Mr. Alfred Penny']\n",
    "RETURN p, r, m\n",
    "```\n",
    "\n",
    "## Removing properties\n",
    "\n",
    "You can remove or delete a property from a node or relationship by using the `REMOVE` keyword, or setting the property to `null`.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Michael Caine' AND m.title = 'The Dark Knight'\n",
    "REMOVE r.roles\n",
    "RETURN p, r, m\n",
    "```\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.name = 'Gene Hackman'\n",
    "SET p.born = null\n",
    "RETURN p\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "baab1397-b747-418b-bb10-32f1e1801fa6",
   "metadata": {},
   "source": [
    "## Customizing MERGE behavior\n",
    "\n",
    "You can also specify behavior at runtime that enables you to set properties when the node is created or when the node is found. We can use the `ON CREATE SET` or `ON MATCH SET` conditions, or the `SET` keywords to set any additional properties.\n",
    "\n",
    "```cypher\n",
    "// Find or create a person with this name\n",
    "MERGE (p:Person {name: 'McKenna Grace'})\n",
    "\n",
    "// Only set the `createdAt` property if the node is created during this query\n",
    "ON CREATE SET p.createdAt = datetime()\n",
    "\n",
    "// Only set the `updatedAt` property if the node was created previously\n",
    "ON MATCH SET p.updatedAt = datetime()\n",
    "\n",
    "// Set the `born` property regardless\n",
    "SET p.born = 2006\n",
    "\n",
    "RETURN p\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "01a37d2e-521b-481f-9f38-8e499e05e93a",
   "metadata": {},
   "source": [
    "## Deleting data\n",
    "\n",
    "In a Neo4j database you can delete:\n",
    "\n",
    "- nodes\n",
    "- relationships\n",
    "- properties\n",
    "- labels\n",
    "\n",
    "### 1. Deleting a node\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Jane Doe'})\n",
    "DELETE p\n",
    "```\n",
    "\n",
    "### 2. Deleting a relationship\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Jane Doe'})-[r:ACTED_IN]->(m:Movie {title: 'The Matrix'})\n",
    "DELETE r\n",
    "RETURN p, m\n",
    "```\n",
    "\n",
    "### 3. Deleting a node and its relationships\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Jane Doe'})\n",
    "DETACH DELETE p\n",
    "```\n",
    "\n",
    "### 4. Deleting everything\n",
    "\n",
    "```cypher\n",
    "MATCH (n)\n",
    "DETACH DELETE n\n",
    "```\n",
    "\n",
    "### 5. Deleting labels\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Jane Doe'})\n",
    "REMOVE p:Developer\n",
    "RETURN p\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c4c0065a-ecc1-442c-a405-449d2a7ac421",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
