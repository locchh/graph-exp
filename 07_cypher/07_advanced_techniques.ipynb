{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "50b2d88d-8783-4127-bfbd-3e5eee3c304b",
   "metadata": {},
   "source": [
    "## What is a subquery?\n",
    "\n",
    "When you execute a series of `MATCH` clauses, all nodes and relationships retrieved are in memory. If the memory requirements for a set of `MATCH` clauses exceed the VM configured, the query will fail.\n",
    "\n",
    "A subquery is a set of Cypher statements that execute within their own scope. A subquery is typically called from an outer enclosing query. Using a subquery, you can limit the number of rows that need to be processed.\n",
    "\n",
    "Here are some important things to know about a subquery:\n",
    "\n",
    "- A subquery returns values referred to by the variables in the `RETURN` clause.\n",
    "\n",
    "- A subquery cannot return variables with the same name used in the enclosing query.\n",
    "\n",
    "- You must explicitly pass in variables from the enclosing query to a subquery.\n",
    "\n",
    "## Performing subqueries with CALL\n",
    "\n",
    "In a `CALL` clause, you specify a query that can return data from the graph or derived from the graph. A set of nodes returned in the CALL clause can be used by the enclosing query.\n",
    "\n",
    "```cypher\n",
    "CALL {\n",
    "   MATCH (m:Movie) WHERE m.year = 2000\n",
    "   RETURN m ORDER BY m.imdbRating DESC LIMIT 10\n",
    "}\n",
    "MATCH  (:User)-[r:RATED]->(m)\n",
    "RETURN m.title, avg(r.rating)\n",
    "```\n",
    "\n",
    "## Passing variables into a subquery\n",
    "\n",
    "Here is an example where the subquery is executed after the initial query and the enclosing query passes a variable, m into the subquery.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "CALL {\n",
    "    WITH m\n",
    "    MATCH (m)<-[r:RATED]-(u:User)\n",
    "     WHERE r.rating = 5\n",
    "    RETURN count(u) AS numReviews\n",
    "}\n",
    "RETURN m.title, numReviews\n",
    "ORDER BY numReviews DESC\n",
    "```\n",
    "\n",
    "In this query:\n",
    "\n",
    "1. The first `MATCH` returns a row for every movie, m in the graph.\n",
    "\n",
    "2. It passes the Movie node, m to the subquery.\n",
    "\n",
    "3. Then within the subquery, the query executes to find all users who gave that movie a rating of 5 and counts them.\n",
    "\n",
    "4. The subquery returns the count.\n",
    "\n",
    "5. Back in the enclosing query, the title is returned, and the count of the number of rows returned from the subquery.\n",
    "\n",
    "## Combining query results with UNION\n",
    "\n",
    "As your queries become more complex, you may need to combine the results of multiple queries. You can do so with `UNION`. With `UNION`, the queries you are combining must return the same number of properties or data\n",
    "\n",
    "Here is a simple example:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie) WHERE m.year = 2000\n",
    "RETURN {type:\"movies\", theMovies: collect(m.title)} AS data\n",
    "UNION ALL\n",
    "MATCH (a:Actor) WHERE a.born.year > 2000\n",
    "RETURN { type:\"actors\", theActors: collect(DISTINCT a.name)} AS data\n",
    "```\n",
    "\n",
    "## Using UNION with subqueries\n",
    "\n",
    "Results of a UNION cannot be directly post-processed. But if you wrap a UNION in a subquery, you can then further process the results.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WITH p LIMIT 100\n",
    "CALL {\n",
    "  WITH p\n",
    "  OPTIONAL MATCH (p)-[:ACTED_IN]->(m:Movie)\n",
    "  RETURN m.title + \": \" + \"Actor\" AS work\n",
    "UNION\n",
    "  WITH p\n",
    "  OPTIONAL MATCH (p)-[:DIRECTED]->(m:Movie)\n",
    "  RETURN m.title+ \": \" +  \"Director\" AS work\n",
    "}\n",
    "RETURN p.name, collect(work)\n",
    "```\n",
    "\n",
    "This query:\n",
    "\n",
    "1. 100 Person nodes are retrieved and passed to the subquery.\n",
    "\n",
    "2. If that Person acted in the movie, its title with the Actor suffix is returned.\n",
    "\n",
    "3. The second part of the subquery does the same for the DIRECTED relationships.\n",
    "\n",
    "4. The work results are combined and collected.\n",
    "\n",
    "5. The result is the name of the person and their Actor or Director titles.\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "df3ee8e7-7613-43a1-bc08-525b14f22171",
   "metadata": {},
   "source": [
    "# Using Parameters\n",
    "\n",
    "## Using Cypher parameters\n",
    "\n",
    "In your Cypher statements, a parameter name begins with the `$` symbol. Here is an example where we have parameterized the query:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = $actorName\n",
    "RETURN m.released AS releaseDate,\n",
    "m.title AS title\n",
    "ORDER BY m.released DESC\n",
    "```\n",
    "\n",
    "## Setting a parameter\n",
    "\n",
    "```cypher\n",
    ":param actorName: 'Tom Hanks'\n",
    "```\n",
    "\n",
    "## Setting Integers\n",
    "\n",
    "```cypher\n",
    ":param number: 10\n",
    "```\n",
    "\n",
    "Instead, to force the number to be an integer, you can use the â‡’ operator.\n",
    "\n",
    "```cypher\n",
    ":param number=> 10\n",
    "```\n",
    "\n",
    "## Setting multiple parameters\n",
    "\n",
    "```cypher\n",
    ":params {actorName: 'Tom Cruise', movieName: 'Top Gun'}\n",
    "```\n",
    "\n",
    "## Using multiple parameters\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = $actorName\n",
    "AND m.title = $movieName\n",
    "RETURN p, m\n",
    "```\n",
    "\n",
    "## Viewing parameters\n",
    "\n",
    "```cypher\n",
    ":params\n",
    "```\n",
    "\n",
    "## Removing parameters\n",
    "\n",
    "```cypher\n",
    ":params {}\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
