{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "ca425b5e-454d-4145-87ff-1c3ac4bd67d6",
   "metadata": {},
   "source": [
    "## Controlling Results\n",
    "\n",
    "### 1. Ordering results\n",
    "\n",
    "Whether you return results as nodes or as properties of nodes, you can specify a property value for the ordering. Strings are ordered by their text values. Boolean true comes before false when ordered. Numeric data (including date and datetime properties) are ordered by their numeric value.\n",
    "\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.born.year = 1980\n",
    "RETURN p.name AS name,\n",
    "p.born AS birthDate\n",
    "ORDER BY p.born\n",
    "```\n",
    "\n",
    "Ordering multiple results:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:DIRECTED | ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Tom Hanks'\n",
    "OR p.name = 'Keanu Reeves'\n",
    "RETURN  m.year, m.title\n",
    "ORDER BY m.year DESC , m.title\n",
    "```\n",
    "\n",
    "\n",
    "### 2. Limiting results\n",
    "\n",
    "Although you can filter queries to reduce the number of results returned, you may also want to limit the number of results returned.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE m.released IS NOT NULL\n",
    "RETURN m.title AS title,\n",
    "m.released AS releaseDate\n",
    "ORDER BY m.released DESC LIMIT 100\n",
    "```\n",
    "\n",
    "### 3. Skipping some results\n",
    "\n",
    "In an ordered result set, you may want to control what results are returned. This is useful in an application where pagination is required.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.born.year = 1980\n",
    "RETURN  p.name as name,\n",
    "p.born AS birthDate\n",
    "ORDER BY p.born SKIP 40 LIMIT 10\n",
    "```\n",
    "\n",
    "### 4. Eliminating duplicate\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:DIRECTED | ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = 'Tom Hanks'\n",
    "RETURN DISTINCT m.title, m.released\n",
    "ORDER BY m.title\n",
    "```\n",
    "\n",
    "### 5. Changing data returned\n",
    "\n",
    "You can always change the data that is returned by performing string or numeric operations on the data.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)\n",
    "WHERE m.title CONTAINS 'Toy Story' AND\n",
    "p.died IS NULL\n",
    "RETURN m.title AS movie,\n",
    "p.name AS actor,\n",
    "p.born AS dob,\n",
    "date().year - p.born.year AS ageThisYear\n",
    "```\n",
    "\n",
    "Here is an example where we concatenate string data returned:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)\n",
    "WHERE m.title CONTAINS 'Toy Story' AND p.died IS NULL\n",
    "RETURN 'Movie: ' + m.title AS movie,\n",
    "p.name AS actor,\n",
    "p.born AS dob,\n",
    "date().year - p.born.year AS ageThisYear\n",
    "```\n",
    "\n",
    "Conditionally changing data returned\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)<-[:ACTED_IN]-(p:Person)\n",
    "WHERE p.name = 'Henry Fonda'\n",
    "RETURN m.title AS movie,\n",
    "CASE\n",
    "WHEN m.year < 1940 THEN 'oldies'\n",
    "WHEN 1940 <= m.year < 1950 THEN 'forties'\n",
    "WHEN 1950 <= m.year < 1960 THEN 'fifties'\n",
    "WHEN 1960 <= m.year < 1970 THEN 'sixties'\n",
    "WHEN 1970 <= m.year < 1980 THEN 'seventies'\n",
    "WHEN 1980 <= m.year < 1990 THEN 'eighties'\n",
    "WHEN 1990 <= m.year < 2000 THEN 'nineties'\n",
    "ELSE  'two-thousands'\n",
    "END\n",
    "AS timeFrame\n",
    "```\n",
    "\n",
    "## Aggregation in Cypher\n",
    "\n",
    "### 1. Using count() to aggregate data\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)\n",
    "RETURN a.name AS actorName,\n",
    "d.name AS directorName,\n",
    "count(*) AS numMovies\n",
    "ORDER BY numMovies DESC\n",
    "```\n",
    "\n",
    "### 2. Returning a list\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "RETURN p.name, [p.born, p.died] AS lifeTime\n",
    "LIMIT 10\n",
    "```\n",
    "\n",
    "### 3. Using collect() to create a list\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "RETURN a.name AS actor,\n",
    "count(*) AS total,\n",
    "collect(m.title) AS movies\n",
    "ORDER BY total DESC LIMIT 10\n",
    "```\n",
    "\n",
    "Rather than collecting the values of the title properties for movies, you can collect the nodes.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name ='Tom Cruise'\n",
    "RETURN collect(m) AS tomCruiseMovies\n",
    "```\n",
    "\n",
    "### 4. Eliminating duplication in lists\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE m.year = 1920\n",
    "RETURN  collect( DISTINCT m.title) AS movies,\n",
    "collect( a.name) AS actors\n",
    "```\n",
    "\n",
    "### 5. Accessing elements of a list\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "RETURN m.title AS movie,\n",
    "collect(a.name)[0] AS castMember,\n",
    "size(collect(a.name)) as castSize\n",
    "```\n",
    "\n",
    "You can also return a slice of a collection.\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person)-[:ACTED_IN]->(m:Movie)\n",
    "RETURN m.title AS movie,\n",
    "collect(a.name)[2..] AS castMember,\n",
    "size(collect(a.name)) as castSize\n",
    "```\n",
    "\n",
    "### 6. Other aggregating functions\n",
    "\n",
    "- `min()`\n",
    "- `max()`\n",
    "- `avg()`\n",
    "- `stddev()`\n",
    "- `sum()`\n",
    "\n",
    "You can either use `count()` to count the number of rows, or alternatively, you can return the size of the collected results. The `size()` function returns the number of elements in a list.\n",
    "\n",
    "### 7. List comprehension\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "RETURN m.title as movie,\n",
    "[x IN m.countries WHERE x CONTAINS 'USA' OR x CONTAINS 'Germany']\n",
    "AS country LIMIT 500\n",
    "```\n",
    "\n",
    "### 8. Pattern comprehension\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE m.year = 2015\n",
    "RETURN m.title,\n",
    "[(dir:Person)-[:DIRECTED]->(m) | dir.name] AS directors,\n",
    "[(actor:Person)-[:ACTED_IN]->(m) | actor.name] AS actors\n",
    "```\n",
    "\n",
    "Notice that for pattern comprehension we specify the list with the square braces to include the pattern followed by the pipe character to then specify what value will be placed in the list from the pattern. `[<pattern> | value]`\n",
    "\n",
    "Here is another example of using pattern comprehension to create a list where we specify a filter for the pattern.\n",
    "\n",
    "```cypher\n",
    "MATCH (a:Person {name: 'Tom Hanks'})\n",
    "RETURN [(a)-->(b:Movie)\n",
    "WHERE b.title CONTAINS \"Toy\" | b.title + \": \" + b.year]\n",
    "AS movies\n",
    "```\n",
    "\n",
    "### 9. Working with maps\n",
    "\n",
    "A Cypher map is list of key/value pairs where each element of the list is of the format 'key': value. A node or relationship can have a property that is a map.\n",
    "\n",
    "```cypher\n",
    "RETURN {Jan: 31, Feb: 28, Mar: 31, Apr: 30 ,\n",
    "May: 31, Jun: 30 , Jul: 31, Aug: 31, Sep: 30,\n",
    "Oct: 31, Nov: 30, Dec: 31}['Feb'] AS daysInFeb\n",
    "```\n",
    "\n",
    "Alternatively, you can access a value with the '.' notation:\n",
    "\n",
    "```cypher\n",
    "RETURN {Jan: 31, Feb: 28, Mar: 31, Apr: 30 ,\n",
    "May: 31, Jun: 30 , Jul: 31, Aug: 31, Sep: 30,\n",
    "Oct: 31, Nov: 30, Dec: 31}.Feb AS daysInFeb\n",
    "```\n",
    "\n",
    "And you can return a list of keys of a map as follows:\n",
    "\n",
    "```cypher\n",
    "RETURN keys({Jan: 31, Feb: 28, Mar: 31, Apr: 30 ,\n",
    "May: 31, Jun: 30 ,Jul: 31, Aug: 31, Sep: 30,\n",
    "Oct: 31, Nov: 30, Dec: 31}) AS months\n",
    "```\n",
    "\n",
    "### 10. Map projections\n",
    "\n",
    "Map projections are when you can use retrieved nodes to create or return some of the information in the nodes.\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)\n",
    "WHERE m.title CONTAINS 'Matrix'\n",
    "RETURN m { .title, .released } AS movie\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "820eefde-b2cf-454d-8a7a-f31f6094ef9f",
   "metadata": {},
   "source": [
    "\n",
    "## Date and Time\n",
    "\n",
    "### 1. Working with date time data\n",
    "\n",
    "Cypher has these basic formats for storing date and time data.\n",
    "\n",
    "```cypher\n",
    "RETURN date(), datetime(), time()\n",
    "```\n",
    "\n",
    "Create a node in the graph containing\n",
    "\n",
    "```cypher\n",
    "MERGE (x:Test {id: 1})\n",
    "SET x.date = date(),\n",
    "    x.datetime = datetime(),\n",
    "    x.time = time()\n",
    "RETURN x\n",
    "```\n",
    "\n",
    "### 2. Extracting components of a date or datetime\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "RETURN x.date.day, x.date.year,\n",
    "x.datetime.year, x.datetime.hour,\n",
    "x.datetime.minute\n",
    "```\n",
    "\n",
    "### 3. Setting date values\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "SET x.date1 = date('2022-01-01'),\n",
    "    x.date2 = date('2022-01-15')\n",
    "RETURN x\n",
    "```\n",
    "\n",
    "### 4. Setting datetime values\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "SET x.datetime1 = datetime('2022-01-04T10:05:20'),\n",
    "    x.datetime2 = datetime('2022-04-09T18:33:05')\n",
    "RETURN x\n",
    "```\n",
    "\n",
    "### 5. Working with durations\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "RETURN duration.between(x.date1,x.date2)\n",
    "```\n",
    "\n",
    "We can return the duration in days between two datetime values\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "RETURN duration.inDays(x.datetime1,x.datetime2).days\n",
    "```\n",
    "\n",
    "We can add a duration of 6 months:\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "RETURN x.date1 + duration({months: 6})\n",
    "```\n",
    "\n",
    "### 6. Using APOC to format dates and times\n",
    "\n",
    "```cypher\n",
    "MATCH (x:Test {id: 1})\n",
    "RETURN x.datetime as Datetime,\n",
    "apoc.temporal.format( x.datetime, 'HH:mm:ss.SSSS')\n",
    "AS formattedDateTime\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
