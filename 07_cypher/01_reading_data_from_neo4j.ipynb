{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5aa250b7-a356-4e9e-9e04-1f95f97fd39f",
   "metadata": {},
   "source": [
    "# Introduction to cypher\n",
    "\n",
    "## What is Cypher?\n",
    "\n",
    "Cypher is a query language designed for graphs.\n",
    "\n",
    "- Nodes are represented by parentheses `()`.\n",
    "- Colon to signify the label(s), for example `(:Person)`.\n",
    "- Relationships between nodes are written with two dashes, for example `(:Person)--(:Movie)`.\n",
    "- The direction of a relationship is indicated using a greater than or less than symbol `<` or `>` , for example `(:Person)-→(:Movie)`.\n",
    "- The type of the relationship is written using the square brackets between the two dashes: `[` and `]`, for example `[:ACTED_IN]`\n",
    "- Properties drawn in a speech bubble are specified in a JSON like syntax. Properties in Neo4j are key/value pairs, for example `{name: 'Tom Hanks'}`.\n",
    "\n",
    "For example, a Cypher pattern in the graph could be:\n",
    "```cypher\n",
    "// example Cypher pattern\n",
    "(m:Movie {title: 'Cloud Atlas'})<-[:ACTED_IN]-(p:Person)\n",
    "```\n",
    "\n",
    "## How Cypher works\n",
    "\n",
    "Cypher works by matching patterns in the data. We retrieve data from the graph using the `MATCH` keyword. You can think of the `MATCH` clause as similar to the `FROM` clause in an SQL statement.\n",
    "\n",
    "For example, if we want to find a Person in the graph, we would `MATCH` a pattern of a single node with a label of `:Person` - prefixed with a colon `:`.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "RETURN p\n",
    "```\n",
    "\n",
    "Now, say we want to find the node which represents the Person who’s name is Tom Hanks. Our Person nodes all have a name property. We can use the braces `{..}` to specify the key/value pair of name and Tom Hanks as the filter. As Tom Hanks is a string, we will need to place it inside single or double quotes.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Tom Hanks'})\n",
    "RETURN p\n",
    "```\n",
    "\n",
    "In our Cypher statement, we can access properties using a dot notation. For example, to return the name property value using its property key p.name.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Tom Hanks'})\n",
    "RETURN  p.born\n",
    "```\n",
    "\n",
    "Another way that you can filter queries is by using the `WHERE` clause, rather than specifying the property value inline with braces.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.name = 'Tom Hanks'\n",
    "RETURN p.born\n",
    "```\n",
    "\n",
    "## Finding Relationships\n",
    "\n",
    "We can extend the pattern in the MATCH clause to traverse through all relationships with a type of `ACTED_IN` to any node. Our domain model shows that the `ACTED_IN` relationship goes in an outgoing direction from the Person node so we can add the direction in our pattern. We often refer to this as a **traversal**.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Tom Hanks'})-[:ACTED_IN]->()\n",
    "```\n",
    "\n",
    "Our data model dictates that the node at the other end of that relationship will be Movie node, so we don’t necessarily need to specify the `:Movie` label in the node - instead we will use the variable m.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(m)\n",
    "RETURN m.title\n",
    "```\n",
    "\n",
    "If our graph had different labels, for example Television and Movie nodes this query would have returned all Television and Movie nodes that Tom Hanks acted in. That is, if we had multiple types of nodes at the end of the ACTED_IN relationships in our graph, we could make sure that we only return movies.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person {name: 'Tom Hanks'})-[:ACTED_IN]->(m:Movie)\n",
    "RETURN m.title\n",
    "```\n",
    "\n",
    "## Filtering Queries\n",
    "\n",
    "You have already learned how you can test equality for properties of a node and how you can use logical expressions to further filter what you want to retrieve.\n",
    "\n",
    "### Filtering by node values\n",
    "\n",
    "For example, this query retrieves the Person nodes and Movie nodes where the person acted in a movie that was released in 2008 or 2009:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE m.released = 2008 OR m.released = 2009\n",
    "RETURN p, m\n",
    "```\n",
    "\n",
    "### Filtering by node labels\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE m.title='The Matrix'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "An alternative to this query is the following where we test the node labels in the WHERE clause:\n",
    "\n",
    "```cypher\n",
    "MATCH (p)-[:ACTED_IN]->(m)\n",
    "WHERE p:Person AND m:Movie AND m.title='The Matrix'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "### Filtering using ranges\n",
    "\n",
    "You can specify a range for filtering a query. Here we want to retrieve Person nodes of people who acted in movies released between 2000 and 2003:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE 2000 <= m.released <= 2003\n",
    "RETURN p.name, m.title, m.released\n",
    "```\n",
    "\n",
    "### Filtering by existence of a property\n",
    "\n",
    "Recall that by default, there is no requirement that a node or relationship has a given property. Here is an example of a query where we only want to return Movie nodes where Jack Nicholson acted in the movie, and the movie has the tagline property.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name='Jack Nicholson' AND m.tagline IS NOT NULL\n",
    "RETURN m.title, m.tagline\n",
    "```\n",
    "\n",
    "### Filtering by partial strings\n",
    "\n",
    "Cypher has a set of string-related keywords that you can use in your `WHERE` clauses to test string property values. You can specify `STARTS WITH`, `ENDS WITH`, and `CONTAINS`.\n",
    "\n",
    "For example, to find all actors in the graph whose first name is Michael, you would write:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->()\n",
    "WHERE p.name STARTS WITH 'Michael'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "String tests are case-sensitive so you may need to use the `toLower()` or `toUpper()` functions to ensure the test yields the correct results. For example:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:ACTED_IN]->()\n",
    "WHERE toLower(p.name) STARTS WITH 'michael'\n",
    "RETURN p.name\n",
    "```\n",
    "\n",
    "### Filtering by patterns in the graph\n",
    "\n",
    "Suppose you wanted to find all people who wrote a movie but did not direct that same movie. Here is how you would perform the query:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[:WROTE]->(m:Movie)\n",
    "WHERE NOT exists( (p)-[:DIRECTED]->(m) )\n",
    "RETURN p.name, m.title\n",
    "```\n",
    "\n",
    "### Filtering using lists\n",
    "\n",
    "If you have a set of values you want to test with, you can place them in a list or you can test with an existing list in the graph. A Cypher list is a comma-separated set of values within square brackets.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "WHERE p.born IN [1965, 1970, 1975]\n",
    "RETURN p.name, p.born\n",
    "```\n",
    "\n",
    "You can also compare a value to an existing list in the graph.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)\n",
    "WHERE  'Neo' IN r.roles AND m.title='The Matrix'\n",
    "RETURN p.name, r.roles\n",
    "```\n",
    "\n",
    "## What properties does a node or relationship have?\n",
    "\n",
    "Discover the keys for the Person nodes in the graph by running this code:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Person)\n",
    "RETURN p.name, keys(p)\n",
    "```\n",
    "\n",
    "## What properties exist in the graph?\n",
    "\n",
    "More generally, you can run this code to return all the property keys defined in the graph.\n",
    "\n",
    "```cypher\n",
    "CALL db.propertyKeys()\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
