{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "2afe1b34-c894-462e-b354-5bcd60d6fb04",
   "metadata": {},
   "source": [
    "# Pipelining Queries\n",
    "\n",
    "## What does pipelining mean?\n",
    "\n",
    "You have learned that you can use the WITH clause to redefine the scope for the query:\n",
    "\n",
    "```cypher\n",
    "WITH  'Tom Hanks' AS theActor\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = theActor\n",
    "WITH m  LIMIT 5\n",
    "// possibly do more with the five m nodes\n",
    "RETURN m.title AS movies\n",
    "```\n",
    "\n",
    "You can do something with the five nodes by adding another `MATCH` clause:\n",
    "\n",
    "```cypher\n",
    "WITH  'Tom Hanks' AS theActor\n",
    "MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.name = theActor\n",
    "WITH m  LIMIT 5\n",
    "MATCH (d:Person)-[:DIRECTED]->(m)\n",
    "RETURN d.name AS director,\n",
    "m.title AS movies\n",
    "```\n",
    "\n",
    "## Using WITH for aggregation\n",
    "\n",
    "A very common use of `WITH` is to aggregate data so that intermediate results can be used to create the final returned data or to pass the intermediate results to the next part of the query.\n",
    "\n",
    "```cypher\n",
    "MATCH (:Movie {title: 'Toy Story'})-[:IN_GENRE]->(g:Genre)<-[:IN_GENRE]-(m)\n",
    "WHERE m.imdbRating IS NOT NULL\n",
    "WITH g.name AS genre,\n",
    "count(m) AS moviesInCommon,\n",
    "sum(m.imdbRating) AS total\n",
    "RETURN genre, moviesInCommon,\n",
    "total/moviesInCommon AS score\n",
    "ORDER By score DESC\n",
    "```\n",
    "\n",
    "Here is another example that shows aggregation and pipelining:\n",
    "\n",
    "```cypher\n",
    "MATCH (u:User {name: \"Misty Williams\"})-[r:RATED]->(:Movie)\n",
    "WITH u, avg(r.rating) AS average\n",
    "MATCH (u)-[r:RATED]->(m:Movie)\n",
    "WHERE r.rating > average\n",
    "RETURN average , m.title AS movie,\n",
    "r.rating as rating\n",
    "ORDER BY rating DESC\n",
    "```\n",
    "\n",
    "## Using WITH for collecting\n",
    "\n",
    "Another common use for the `WITH` clause is to collect results into a list that will be returned:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)--(a:Actor)\n",
    "WHERE m.title CONTAINS 'New York'\n",
    "WITH m, collect (a.name) AS actors,\n",
    "count(*) AS numActors\n",
    "RETURN m.title AS movieTitle, actors\n",
    "ORDER BY numActors DESC\n",
    "```\n",
    "\n",
    "Here is another example where we perform a 2-step aggregation for collecting a list of maps:\n",
    "\n",
    "```cypher\n",
    "MATCH (m:Movie)<-[:ACTED_IN]-(a:Actor)\n",
    "WHERE m.title CONTAINS 'New York'\n",
    "WITH m, collect (a.name) AS actors,\n",
    "count(*) AS numActors\n",
    "ORDER BY numActors DESC\n",
    "RETURN collect(m { .title, actors, numActors }) AS movies\n",
    "```\n",
    "\n",
    "## Using LIMIT early\n",
    "\n",
    "A best practice is to execute queries that minimize the number of rows processed in the query. One way to do that is to limit early in the query. This also helps in reducing the number of properties loaded from the database too early.\n",
    "\n",
    "```cypher\n",
    "PROFILE MATCH (p:Person)-[:ACTED_IN]->(m:Movie)\n",
    "WHERE p.born.year = 1980\n",
    "WITH p,\n",
    "collect(m.title) AS movies LIMIT 3\n",
    "RETURN p.name AS actor, movies\n",
    "```\n",
    "\n",
    "A better way to do this query is to limit early. This query will perform slightly better because LIMIT is moved up in the query:\n",
    "\n",
    "```cypher\n",
    "PROFILE MATCH (p:Actor)\n",
    "WHERE p.born.year = 1980\n",
    "WITH p  LIMIT 3\n",
    "MATCH (p)-[:ACTED_IN]->(m:Movie)\n",
    "WITH p, collect(m.title) AS movies\n",
    "RETURN p.name AS actor,  movies\n",
    "```\n",
    "\n",
    "## Use DISTINCT when necessary\n",
    "\n",
    "Here is an example of a query that retrieves three actors, then collects the names of the genres for the movies that actor acted in.\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Actor)\n",
    "WHERE p.born.year = 1980\n",
    "WITH p  LIMIT 3\n",
    "MATCH (p)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre)\n",
    "WITH p, collect(g.name) AS genres\n",
    "RETURN p.name AS actor, genres\n",
    "```\n",
    "\n",
    "Notice that for this query, the collected genre names are repeated. You would want to ensure that they are not duplicated as follows:\n",
    "\n",
    "```cypher\n",
    "MATCH (p:Actor)\n",
    "WHERE p.born.year = 1980\n",
    "WITH p  LIMIT 3\n",
    "MATCH (p)-[:ACTED_IN]->(m:Movie)-[:IN_GENRE]->(g:Genre)\n",
    "WITH p, collect(DISTINCT g.name) AS genres\n",
    "RETURN p.name AS actor, genres\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.12.11"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
